plugins {
  id 'org.ajoberstar.grgit' version '1.4.2'
}

apply plugin: 'java'
apply plugin: 'eclipse'

import org.ajoberstar.grgit.*
import org.apache.tools.ant.filters.ReplaceTokens

version = generateVersion('1.0')
String pluginMain = 'com.lesserhydra.secondchance.SecondChance'

allprojects {
  apply plugin: 'java'
  sourceCompatibility = '1.8'
  repositories {
    mavenLocal()
    mavenCentral()
    maven {url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'}
  }
}

dependencies {
  //Bukkit api
  compile 'org.bukkit:bukkit:1.8-R0.1-SNAPSHOT'

  //Compatibility modules
  compile project(':compatibility')

  //Testing libraries
  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:3.4.+'
  testCompile 'org.powermock:powermock-core:1.6.+'
  testCompile 'org.powermock:powermock-api-mockito:1.6.+'
  testCompile 'org.powermock:powermock-module-junit4:1.6.+'
  testCompile 'org.mockito:mockito-all:1.10.+'
  testCompile 'org.bukkit:craftbukkit:1.10.2-R0.1-SNAPSHOT'
}

//Add compatibility modules to jar
jar {
  from project(':compatibility').sourceSets.main.output.classesDir
  from project(':compatibility').sourceSets.main.output.resourcesDir
}

//Generate plugin.yml
processResources {
  inputs.file 'build.gradle' //So it's regenerated if version changes
  from 'templates/plugin.yml'
  filter(ReplaceTokens, tokens: [
    version:		version,
    mainClass:  pluginMain,
  ])
}

static String generateVersion(workingVersion) {
  Grgit repo = Grgit.open()
  Tag currentTag = repo.tag.list().find {it.commit == repo.head()}

  if (currentTag == null) return workingVersion + '-SNAPSHOT'
  return currentTag.getName()
}
