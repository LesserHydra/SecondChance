plugins {
  id 'org.ajoberstar.grgit' version '1.4.2'
}

apply plugin: 'java'
apply plugin: 'eclipse'

import org.ajoberstar.grgit.*
import org.apache.tools.ant.filters.ReplaceTokens

version = generateVersion('0.3')
String pluginName = 'SecondChance'
String pluginMain = 'com.lesserhydra.secondchance.SecondChance'
String author = 'Roboboy'

repositories {
  mavenLocal()
}

dependencies {
	compile group: 'org.spigotmc', name: 'spigot', version: '1.9.2-R0.1-SNAPSHOT'
}

task generatePluginYaml(type: Copy) {
  from 'templates/plugin.yml'
  into 'src/main/resources/'
  filter(ReplaceTokens, tokens: [
		version:		version,
    pluginName: pluginName,
    mainClass:  pluginMain,
    author:			author,
  ])
}

task removePluginYaml(type: Delete) {
	delete 'src/main/resources/plugin.yml'
}

generatePluginYaml.dependsOn('removePluginYaml')
processResources.dependsOn('generatePluginYaml')
clean.dependsOn('removePluginYaml')

String generateVersion(workingVersion) {
  Grgit repo = Grgit.open(project.file('.'))
  Tag currentTag = repo.tag.list().find {it.commit == repo.head()}

  if (currentTag == null) return workingVersion + '-' + repo.head().abbreviatedId
  return currentTag.getName()
}
